PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wsi: <http://whostarredin.com/data/>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
	?film wdt:P577	?date ;
		rdfs:label	?label	;
		wdt:P161	?actor ;
		schema:about ?cleanedWikipediaLink ;
		wdt:P57	?director
} WHERE {
	?film wdt:P31 wd:Q11424 ;
		rdfs:label	?label

  OPTIONAL {
	?film wdt:P577	?date
  }
	OPTIONAL {
	?film  wdt:P161 ?actor
	}
	OPTIONAL {
	?film wdt:P57	?director
	}
	OPTIONAL {
  	?wikipediaLink schema:about ?film
    FILTER regex(str(?wikipediaLink), "https://en.wikipedia.org/wiki/.*", "i")
	BIND(replace(str(?wikipediaLink), "https://en.wikipedia.org/wiki/", "") AS ?cleanedWikipediaLink)
    }
  FILTER(LANG(?label) = "en" || LANG(?label) = "" )
}
